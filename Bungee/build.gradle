plugins {
    id "java"
}

archivesBaseName = "bungee"
version = project.bungee_plugin_version

repositories {
    maven { url "https://papermc.io/repo/repository/maven-public/" }
    maven { url "https://repo.viaversion.com" }
    maven { url "https://jitpack.io/" }
}

dependencies {
    dependency project(":Shared")
    dependency project(path: ":Shared", configuration: "dependency")

    compileOnly "io.github.waterfallmc:waterfall-api:${project.waterfall_version}"

    compileOnly "net.luckperms:api:${project.luckperms_version}"
    compileOnly "com.viaversion:viaversion-api:${project.viaversion_version}"
    compileOnly "com.github.NuVotifier:NuVotifier:${project.votifier_version}"
}

jar {
    sourceCompatibility = JavaVersion.toVersion(project.java_version)
    targetCompatibility = JavaVersion.toVersion(project.java_version)

    duplicatesStrategy = "exclude"
    archiveVersion = rootProject.version + "-" + project.version

    from files(project.sourceSets.main.output)
    from files(project.configurations.dependency.collect { it.isDirectory() ? it : zipTree(it) })
}

processResources {
	inputs.property "plugin_version", rootProject.version + "-" + project.version

    filesMatching("bungee.yml") {
        expand "plugin_version": rootProject.version + "-" + project.version
    }
}