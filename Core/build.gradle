plugins {
    id "java"
}

version = core_version

repositories {
    maven {
        url "https://maven.kaleko.dev/public-snapshot/"
    }
    maven {
        url "https://papermc.io/repo/repository/maven-public/"
    }
    mavenCentral()
}

dependencies {
    implementation project(":Messaging")

    implementation "io.github.kale_ko:ejcl:${ejcl_version}"

    compileOnly "io.papermc.paper:paper-api:${paper_version}"
    compileOnly "com.velocitypowered:velocity-api:${velocity_version}"
    compileOnly "io.netty:netty-all:${netty_version}"

    implementation "org.mariadb.jdbc:mariadb-java-client:${mariadb_connector_version}"

    implementation "org.slf4j:slf4j-api:${slf4j_version}"
}

tasks.register("fatJar", Jar) {
    archiveClassifier = "fat"

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from(sourceSets.main.output)
    from(configurations.runtimeClasspath.collect {
        it.isDirectory() ? null : zipTree(it)
    })
}

build {
    finalizedBy tasks.fatJar
}

processResources {
    inputs.property "version", core_version

    filesMatching(["paper-plugin.yml", "velocity-plugin.json"]) {
        expand "version": core_version
    }
}