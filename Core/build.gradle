plugins {
    id "java"
}

version = project.core_version

repositories {
    mavenCentral()
    maven {
        url "https://papermc.io/repo/repository/maven-public/"
    }
    maven {
        url "https://jitpack.io"
    }
}

dependencies {
    implementation "com.github.Kale-Ko:EJCL:${project.ejcl_version}"

    compileOnly "io.github.waterfallmc:waterfall-api:${project.waterfall_version}"
    compileOnly "io.papermc.paper:paper-api:${project.paper_version}"
    compileOnly "org.slf4j:slf4j-api:2.0.9"
}

tasks.register("fatJar", Jar) {
    archiveClassifier = "fat"

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from(sourceSets.main.output)
    from(configurations.runtimeClasspath.collect {
        it.isDirectory() ? null : zipTree(it)
    })
}

build {
    finalizedBy tasks.fatJar
}

processResources {
    inputs.property "version", project.version

    filesMatching(["bungee.yml", "plugin.yml", "limbo.yml"]) {
        expand "version": project.version
    }
}