plugins {
    id "java"
}

version = worldguard_version

repositories {
    maven {
        url "https://maven.kaleko.dev/public-snapshot/"
    }
    maven {
        url "https://papermc.io/repo/repository/maven-public/"
    }
    mavenCentral()
}

dependencies {
    compileOnly project(":Core")
    compileOnly project(":Global")

    implementation "io.github.kale_ko:ejcl:${ejcl_version}"

    compileOnly "io.papermc.paper:paper-api:${paper_version}"

    compileOnly "org.slf4j:slf4j-api:${slf4j_version}"
}

tasks.register("fatJar", Jar) {
    archiveClassifier = "fat"

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from(sourceSets.main.output)
    from(configurations.runtimeClasspath.collect {
        it.isDirectory() ? null : zipTree(it)
    })
}

build {
    finalizedBy tasks.fatJar
}

processResources {
    inputs.property "version", worldguard_version

    filesMatching("evercraft.yml") {
        expand "version": worldguard_version
    }
}