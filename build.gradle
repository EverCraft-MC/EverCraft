plugins {
    id "java"
}

project.group = "io.github.evercraftmc"
project.version = project.core_version

repositories {
    mavenCentral()
}

subprojects {
    group = "io.github.evercraftmc.evercraft"

    repositories {
        mavenCentral()
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(project.java_version)
        }
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = "UTF-8"
    }
}

tasks.register("upload") {
    dependsOn subprojects.collect {
        it.tasks.fatJar
    }

    doLast {
        def path = System.getenv("EVERCRAFT_PATH")
        def servers = System.getenv("EVERCRAFT_SERVERS").split(";")

        servers.each() {
            def server = it

            if ((System.getProperty("type") == null || System.getProperty("type").equalsIgnoreCase(server.split(":")[1])) && findProject(":" + server.split(":")[1]) != null) {
                exec {
                    commandLine "rsync", "-q", project(":" + server.split(":")[1]).tasks.fatJar.outputs.files[0].toPath(), path + server.split(":")[0] + server.split(":")[2] + "/EverCraft-" + server.split(":")[1] + ".jar"
                }
            }
        }
    }
}